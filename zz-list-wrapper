emulate -LR zsh

_zz_wrapper_has_terminfo=0

zmodload zsh/curses
zmodload zsh/terminfo 2>/dev/null && _zz_wrapper_has_terminfo=1

trap "_zz_wrapper_on_exit" EXIT

autoload zz-list zz-list-input zz-list-draw

integer term_height="$LINES"
integer term_width="$COLUMNS"

# Reason for this function is that on some systems
# smcup and rmcup are not knowing why left empty
_zz_wrapper_alternate_screen() {
    if [ "$_zz_wrapper_has_terminfo" = "1" ]; then
        [[ "$1" = "1" && -n "$terminfo[smcup]" ]] && return
        [[ "$1" = "0" && -n "$terminfo[rmcup]" ]] && return
    fi

    case "$TERM" in
        *rxvt*)
            [ "$1" = "1" ] && echo -n $'\x1b7\x1b[?47h'
            [ "$1" = "0" ] && echo -n $'\x1b[2J\x1b[?47l\x1b8'
            ;;
        *)
            [ "$1" = "1" ] && echo -n $'\x1b[?1049h'
            [ "$1" = "0" ] && echo -n $'\x1b[?1049l'
            # just to remember two other that work: $'\x1b7\x1b[r\x1b[?47h', $'\x1b[?47l\x1b8'
            ;;
    esac
}

# Cleanup before any exit
_zz_wrapper_on_exit() {
    _zz_wrapper_end
    _zz_wrapper_alternate_screen 0
    _zzlist_cursor_visibility "plain" 1
    unset _zzlist_has_terminfo
}

_zz_wrapper_init_windows() {
    _zz_wrapper_alternate_screen 1
    zcurses init
    zcurses delwin cmd 2>/dev/null
    zcurses delwin main 2>/dev/null
    zcurses delwin status 2>/dev/null
    zcurses addwin main $(( term_height - 3 )) "$term_width" 0 0
    zcurses addwin status "3" "$term_width" $(( term_height - 3 )) 0

    bold=1
    zcurses bg main "+bold" white/black
    zcurses bg status "+bold" white/black
}

_zz_wrapper_end() {
    zcurses init
    zcurses delwin cmd 2>/dev/null
    zcurses delwin main 2>/dev/null
    zcurses end
}

_zz_wrapper_init_windows

zcurses border main
zcurses border status

zz-list "main" $(( term_height-3 )) $term_width "status" "3" "$term_width" "  Man:  " "1" "$@"

answer=${reply[REPLY]}
tmp=( "${(z)answer}" )
[[ "$ZZLIST_CURRENT_SEGMENT" -gt "${#tmp}" ]] && ZZLIST_CURRENT_SEGMENT="${#tmp}"
ZZLIST_WRAPPER_BIT=$tmp[ZZLIST_CURRENT_SEGMENT]

# vim:ft=zsh
