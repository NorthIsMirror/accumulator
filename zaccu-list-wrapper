emulate -LR zsh
setopt extendedglob

_zaccu_wrapper_has_terminfo=0

zmodload zsh/curses
zmodload zsh/terminfo 2>/dev/null && _zaccu_wrapper_has_terminfo=1

trap "_zaccu_wrapper_on_exit" EXIT

autoload zaccu-list zaccu-list-input zaccu-list-draw

integer term_height="$LINES"
integer term_width="$COLUMNS"

# Reason for this function is that on some systems
# smcup and rmcup are not knowing why left empty
_zaccu_wrapper_alternate_screen() {
    if [ "$_zaccu_wrapper_has_terminfo" = "1" ]; then
        [[ "$1" = "1" && -n "$terminfo[smcup]" ]] && return
        [[ "$1" = "0" && -n "$terminfo[rmcup]" ]] && return
    fi

    case "$TERM" in
        *rxvt*)
            [ "$1" = "1" ] && echo -n $'\x1b7\x1b[?47h'
            [ "$1" = "0" ] && echo -n $'\x1b[2J\x1b[?47l\x1b8'
            ;;
        *)
            [ "$1" = "1" ] && echo -n $'\x1b[?1049h'
            [ "$1" = "0" ] && echo -n $'\x1b[?1049l'
            # just to remember two other that work: $'\x1b7\x1b[r\x1b[?47h', $'\x1b[?47l\x1b8'
            ;;
    esac
}

# Cleanup before any exit
_zaccu_wrapper_on_exit() {
    _zaccu_wrapper_end
    _zaccu_wrapper_alternate_screen 0
    _zacculist_cursor_visibility "plain" 1
    unset _zacculist_has_terminfo
}

_zaccu_wrapper_init_windows() {
    _zaccu_wrapper_alternate_screen 1
    zcurses init
    zcurses delwin cmd 2>/dev/null
    zcurses delwin main 2>/dev/null
    zcurses delwin status 2>/dev/null
    zcurses addwin main $(( term_height - 3 )) "$term_width" 0 0
    zcurses addwin status "3" "$term_width" $(( term_height - 3 )) 0

    bold=1
    zcurses bg main "+bold" white/black
    zcurses bg status "+bold" white/black
}

_zaccu_wrapper_end() {
    zcurses init
    zcurses delwin cmd 2>/dev/null
    zcurses delwin main 2>/dev/null
    zcurses end
}

_zaccu_wrapper_init_windows

zcurses border main
zcurses border status

zaccu-list "main" $(( term_height-3 )) $term_width "status" "3" "$term_width" "  Accumulated data  " "1" "$@"

if [[ "$REPLY" != "-1" && "$REPLY" = -(#c0,1)[0-9]## ]]; then
    answer=${reply[REPLY]}
    tmp=( "${(z)answer}" )
    [[ "$ZACCULIST_CURRENT_SEGMENT" -gt "${#tmp}" ]] && ZACCULIST_CURRENT_SEGMENT="${#tmp}"
    ZACCULIST_WRAPPER_BIT=$tmp[ZACCULIST_CURRENT_SEGMENT]
else
    ZACCULIST_WRAPPER_BIT=""
fi

# vim:ft=zsh
