emulate -L zsh

zmodload zsh/curses

setopt typesetsilent

# Compute first to show index
_zacculist_compute_first_to_show_idx() {
    from_what_idx_list_is_shown=0+((current_idx-1)/page_height)*page_height+1
}

typeset -ga reply
reply=( -1 '' )
integer current_idx="$1"
integer from_what_idx_list_is_shown="$2"
integer page_height="$3"
integer page_width="$4"
integer last_element="$5"
integer nseg="$6"
integer hscroll="$7"
local key="$8"
integer search="$9"
local buffer="$10"
integer uniq_mode="$11"
integer disable_search="$12"

#
# Listening for input
#

if [ "$search" = "0" ]; then

case "$key" in
    (UP|k|$'\C-P')
        # Are there any elements before the current one?
        [ "$current_idx" -gt 1 ] && current_idx=current_idx-1;
        _zacculist_compute_first_to_show_idx
        ;;
    (DOWN|j|$'\C-N')
        # Are there any elements after the current one?
        [ "$current_idx" -lt "$last_element" ] && current_idx=current_idx+1;
        _zacculist_compute_first_to_show_idx
        ;;
    (PPAGE|$'\b'|$'\C-?'|BACKSPACE)
        current_idx=current_idx-page_height
        [ "$current_idx" -lt 1 ] && current_idx=1;
        _zacculist_compute_first_to_show_idx
        ;;
    (NPAGE|" ")
        current_idx=current_idx+page_height
        [ "$current_idx" -gt "$last_element" ] && current_idx=last_element;
        _zacculist_compute_first_to_show_idx
        ;;
    ($'\C-U')
        current_idx=current_idx-page_height/2
        [ "$current_idx" -lt 1 ] && current_idx=1;
        _zacculist_compute_first_to_show_idx
        ;;
    ($'\C-D')
        current_idx=current_idx+page_height/2
        [ "$current_idx" -gt "$last_element" ] && current_idx=last_element;
        _zacculist_compute_first_to_show_idx
        ;;
    (HOME|g)
        current_idx=1
        _zacculist_compute_first_to_show_idx
        ;;
    (END|G)
        current_idx=last_element
        _zacculist_compute_first_to_show_idx
        ;;
    ($'\n'|ENTER)
        # Is that element selectable?
        # Check for this only when there is no search
        if [[ "$ZACCULIST_SEARCH_BUFFER" != "" || "$ZACCULIST_IS_UNIQ_MODE" -eq 1 ||
            ${ZACCULIST_NONSELECTABLE_ELEMENTS[(r)$current_idx]} != $current_idx ]]
        then
            # Save current element in the result variable
            reply=( $current_idx SELECT )
        fi
        ;;
    (q)
        reply=( -1 QUIT )
        ;;
    (/)
        if [ "$disable_search" = "0" ]; then
            search=1
            _zacculist_cursor_visibility 1
        fi
        ;;
    ($'\t')
        reply=( $current_idx LEAVE )
        ;;
    ($'\C-L')
        reply=( -1 REDRAW )
        ;;
    (\])
        if [[ "${ZACCULIST_ENABLED_EVENTS[(r)BR_MOVE_RIGHT]}" = "BR_MOVE_RIGHT" ]]; then
            reply=( "$ZACCULIST_CURRENT_SEGMENT" "BR_MOVE_RIGHT" )
        else

        [[ "${(t)ZACCULIST_HOP_INDEXES}" = "array" || "${(t)ZACCULIST_HOP_INDEXES}" = "array-local" ]] &&
        [ -z "$ZACCULIST_SEARCH_BUFFER" ] && [ "$ZACCULIST_IS_UNIQ_MODE" -eq 0 ] &&
        for idx in "${(n)ZACCULIST_HOP_INDEXES[@]}"; do
            if [ "$idx" -gt "$current_idx" ]; then
                current_idx=$idx
                _zacculist_compute_first_to_show_idx
                break
            fi
        done

        fi
        ;;
    (\[)
        if [[ "${ZACCULIST_ENABLED_EVENTS[(r)BR_MOVE_LEFT]}" = "BR_MOVE_LEFT" ]]; then
            reply=( "$ZACCULIST_CURRENT_SEGMENT" "BR_MOVE_LEFT" )
        else

        [[ "${(t)ZACCULIST_HOP_INDEXES}" = "array" || "${(t)ZACCULIST_HOP_INDEXES}" = "array-local" ]] &&
        [ -z "$ZACCULIST_SEARCH_BUFFER" ] && [ "$ZACCULIST_IS_UNIQ_MODE" -eq 0 ] &&
        for idx in "${(nO)ZACCULIST_HOP_INDEXES[@]}"; do
            if [ "$idx" -lt "$current_idx" ]; then
                current_idx=$idx
                _zacculist_compute_first_to_show_idx
                break
            fi
        done

        fi
        ;;
    ('<'|'{')
        hscroll=hscroll-7
        [ "$hscroll" -lt 0 ] && hscroll=0
        ;;
    ('>'|'}')
        hscroll+=7
        ;;
    (LEFT|'h')
        if [ "$ZACCULIST_CURRENT_SEGMENT" -gt "$nseg" ]; then
            (( ZACCULIST_CURRENT_SEGMENT = nseg - 1 ))
            [ "$ZACCULIST_CURRENT_SEGMENT" -le 0 ] && ZACCULIST_CURRENT_SEGMENT=1
        else
            (( ZACCULIST_CURRENT_SEGMENT = ZACCULIST_CURRENT_SEGMENT - 1 ))
        fi
        ;;
    (RIGHT|'l')
        (( ZACCULIST_CURRENT_SEGMENT = ZACCULIST_CURRENT_SEGMENT + 1 ))
        [ "$ZACCULIST_CURRENT_SEGMENT" -gt "$nseg" ] && ZACCULIST_CURRENT_SEGMENT="$nseg"
        ;;
    (SLEFT|'H')
        reply=( "$ZACCULIST_CURRENT_SEGMENT" MOVE_LEFT )
        ;;
    (SRIGHT|'L')
        reply=( "$ZACCULIST_CURRENT_SEGMENT" MOVE_RIGHT )
        ;;
    (F1|F2|F3|F4)
        reply=( -1 "$key" )
        ;;
    ($'\E')
        buffer=""
        ;;
    (o|$'\C-O')
        uniq_mode=1-uniq_mode
        ;;
    (*)
        ;;
esac

else

case "$key" in
    ($'\n'|ENTER)
        search=0
        _zacculist_cursor_visibility 0
        ;;
    ($'\C-L')
        reply=( -1 REDRAW )
        ;;

    #
    # Slightly limited navigation
    #

    (UP|$'\C-P')
        [ "$current_idx" -gt 1 ] && current_idx=current_idx-1;
        _zacculist_compute_first_to_show_idx
        ;;
    (DOWN|$'\C-N')
        [ "$current_idx" -lt "$last_element" ] && current_idx=current_idx+1;
        _zacculist_compute_first_to_show_idx
        ;;
    (PPAGE)
        current_idx=current_idx-page_height
        [ "$current_idx" -lt 1 ] && current_idx=1;
        _zacculist_compute_first_to_show_idx
        ;;
    (NPAGE)
        current_idx=current_idx+page_height
        [ "$current_idx" -gt "$last_element" ] && current_idx=last_element;
        _zacculist_compute_first_to_show_idx
        ;;
    ($'\C-U')
        current_idx=current_idx-page_height/2
        [ "$current_idx" -lt 1 ] && current_idx=1;
        _zacculist_compute_first_to_show_idx
        ;;
    ($'\C-D')
        current_idx=current_idx+page_height/2
        [ "$current_idx" -gt "$last_element" ] && current_idx=last_element;
        _zacculist_compute_first_to_show_idx
        ;;
    (HOME)
        current_idx=1
        _zacculist_compute_first_to_show_idx
        ;;
    (END)
        current_idx=last_element
        _zacculist_compute_first_to_show_idx
        ;;
    (LEFT)
        if [ "$ZACCULIST_CURRENT_SEGMENT" -gt "$nseg" ]; then
            (( ZACCULIST_CURRENT_SEGMENT = nseg - 1 ))
            [ "$ZACCULIST_CURRENT_SEGMENT" -le 0 ] && ZACCULIST_CURRENT_SEGMENT=1
        else
            (( ZACCULIST_CURRENT_SEGMENT = ZACCULIST_CURRENT_SEGMENT - 1 ))
        fi
        ;;
    (RIGHT)
        (( ZACCULIST_CURRENT_SEGMENT = ZACCULIST_CURRENT_SEGMENT + 1 ))
        [ "$ZACCULIST_CURRENT_SEGMENT" -gt "$nseg" ] && ZACCULIST_CURRENT_SEGMENT="$nseg"
        ;;
    (F1|F2|F3|F4)
        reply=( -1 "$key" )
        ;;
    (F4|F5|F6|F7|F8|F9|F10)
        # ignore
        ;;

    #
    # The input
    #

    ($'\b'|$'\C-?'|BACKSPACE)
        buffer="${buffer%?}"
        ;;
    ($'\C-W')
        [ "$buffer" = "${buffer% *}" ] && buffer="" || buffer="${buffer% *}"
        ;;
    ($'\C-K')
        buffer=""
        ;;
    ($'\E')
        buffer=""
        search=0
        _zacculist_cursor_visibility 0
        ;;
    ($'\C-O')
        uniq_mode=1-uniq_mode
        ;;
    (*)
        if [[ $#key == 1 && $((#key)) -lt 31 ]]; then
            # ignore all other control keys
        else
            buffer+="$key"
        fi
        ;;
esac

fi

reply[3]="$current_idx"
reply[4]="$from_what_idx_list_is_shown"
reply[5]="$hscroll"
reply[6]="$search"
reply[7]="$buffer"
reply[8]="$uniq_mode"

# vim: set filetype=zsh:
