# Accumulator plugin for VIM. Commands like "vim ~/.zshrc" are
# routed to specified generator function. The generator creates
# text with hyperlinks and appends the text to $SECTIONNAME of
# choice in ZACCU_OUTPUT_DOCUMENT_SECTIONS[$SECTIONNAME] (global
# hash). Initial and final generators manage header text.

# Plugin functions are run from "accu" function, which has
# emulate -LR zsh


#
# Initial generator
#

# No arguments
accu_initial_generator_vim1() {
    ZACCU_OUTPUT_DOCUMENT_SECTIONS[vim1]="${C_GREEN}Vim last opened documents:${C_GREEN_E}"$'\n'
    accu_vim1_was_content=0
}


#
# Generator
#

# $1 - present working directory
# $2 - it's "vim", the command that's invoked
# $3, $4, ... - arguments passed to vim
accu_generator_vim1() {
    local active_path="$1"
    local -a args
    shift 2
    args=( "$@" )

    local a
    for a in "${args[@]}"; do
        # Skip options
        [[ "$a" = -[[:alnum:]]# ]] && continue

        if [ "$accu_vim1_path_occured[$a]" != "1" ]; then
            accu_vim1_was_content=1
            accu_vim1_path_occured[$a]="1"

            zaccu_resolve_path "$active_path" "$a"
            local dir="${reply[1]}" file="${reply[2]}"

            # Truncate to 25 characters, right-pad to 30 characters
            file='%25>...>'"$file"
            file="$C_YELLOW${(%)file}$C_YELLOW_E"
            file="${(r:30:: :)file}"

            reply=()

            # ID, data1, data2, data3, text, handler
            zaccu_get_std_button "vim1" 0 "$active_path" "$a" "$file" accu_std_action_vim1

            # ID, data1, data2, data3, text, handler
            zaccu_get_button "vim1A" 0 "$active_path" "$a" "CD" accu_action_cd_vim1

            # ID, data1, data2, data3, text, handler
            zaccu_get_button "vim1B" 0 "$active_path" "$a" "Backup" accu_action_backup_vim1

            ZACCU_OUTPUT_DOCUMENT_SECTIONS[vim1]+=$'\t'"$reply[1] ${reply[2]} ${reply[3]}"$'\n'
        fi
    done
}


#
# Final generator
#

accu_final_generator_vim1() {
    accu_vim1_path_occured=()

    # No content -> remove header
    if (( accu_vim1_was_content == 0 )); then
        unset 'ZACCU_OUTPUT_DOCUMENT_SECTIONS[vim1]'
    fi
}


#
# Actions
#

accu_std_action_vim1() {
    echo "Std action"
}

accu_action_cd_vim1() {
    echo "Cd action"
}

accu_action_backup_vim1() {
    echo "Backup action"
}


#
# Register generators for command "vim", "gvim"
#

zaccu_register_plugin "vim" accu_initial_generator_vim1 accu_generator_vim1 accu_final_generator_vim1
zaccu_register_plugin "gvim" accu_initial_generator_vim1 accu_generator_vim1 accu_final_generator_vim1


#
# Helper variables
#

# Detects if a path already occured
typeset -gA accu_vim1_path_occured

# Detects whether any content occured
integer accu_vim1_was_content

# vim: ft=zsh
